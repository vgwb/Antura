// FR-01 Paris
// Words used: Eiffel Tower, ticket, coin, Notre-Dame, Louvre, Mona Lisa, Liberty Leading the People, baguette, flour, water, yeast, salt, map, elevator, iron
// activities in this quest:
// - collect ingredients (flour, water, yeast, salt)
// - visit museum (Louvre, Mona Lisa)
// - explore landmarks (Eiffel Tower, Notre-Dame)

title: init
position: -304,-431
tags: type=Start
color: red
---
<<set $TOTAL_COINS = 0>>
<<set $COLLECTED_ITEMS = 0>>
<<declare $QUEST_ITEMS = 0>>
<<declare $MET_GUIDE = false>>
<<declare $MET_MAJOR = false>>
<<declare $MET_MONALISA = false>>
===

title: start
position: -301,-268
tags: NEXTTARGET=TUTOR
---
Welcome to Paris!
===



title: talk_tutor
position: 17,-670
actor: tutor
---
TUTOR: I saw Antura going to the Eiffel Tower.
<<camera_focus tour_eiffell>>
If you don't know where to go, follow the light beam in the sky or use the map!
Get there as soon as possible!
===


title: talk_louvre_external
position: 62,57
group: louvre
---
<<if $MET_MONALISA>>
    <<jump go_bakery>>
<<else>>
<<asset louvre>>
OLD_WOMAN: This is the entrance to the Louvre, our national art museum.
OLD_WOMAN: Do you want to enter?
-> Yes:
    Enjoy your visit!
    <<action AREA_LOUVRE_ENTER >>
-> No:
    Okay.
<<endif>>
===

title: talk_eiffell_roof
position: 46,-448
group: toureiffel
tags: actorUID=GUIDE
---
<<asset  toureiffell>>
GUIDE: The Eiffel Tower is 300 meters tall.
<<asset mr_eiffel>>
GUIDE: Built by Mr Eiffel in 1887.
<<asset iron>>
GUIDE: It's made of iron!
GUIDE: I saw Antura going towards Notre Dame.
<<camera_focus notredame>>
GUIDE: Get there!

===


title: talk_eiffell_guide
position: 311,-447
group: toureiffel
---
<<if $TOTAL_COINS > 2>>
    Here is your ticket.
    <<asset tour_eiffell_ticket>>
    <<set $TOTAL_COINS = $TOTAL_COINS-3>>
    
    <<action  area_toureiffel>>
    <<asset tour_eiffell_map>>
    I saw Antura going up to the top of the tower.
    Take the elevator!
<<elseif $TOTAL_COINS > 0>> 
    TUTOR: Collect all coins!
<<else>>
    GUIDE: Hello. The ticket for the Eiffel Tower costs 3 coins.
<<endif>>
===

title: talk_notre_dame
position: 54,-207
group: notredame
tags: actorUID=OLD_MAN, asset=notredame
---
OLD_MAN: I'm the Mayor of Paris.
OLD_MAN: This is Notre-Dame Cathedral.
<<asset notredame>>
OLD_MAN: It is a famous Gothic church, built in 1182.
<<action AREA_NOTREDAME_ROOF>>
OLD_MAN: Come with me up to the roof of the church!
<<set $MET_MAJOR = true>>
===

title: talk_cook
position: 76,972
group: bakery
---
<<if $COLLECTED_ITEMS >= 4>>
    CRAZY_MAN: GREAT! I can now bake the Baguette. And...
    CRAZY_MAN: CONGRATULATIONS! You won the game! Did you like it?
    <<quest_end 3>>
<<else>>
    Help me! Antura made a mess in my kitchen!
    I can't find the ingredients to make the baguette.
    <<asset  baguette>>
    Our special French bread!
    <<set $QUEST_ITEMS = 4>>
    Please bring me 4 ingredients:
    flour, water, yeast, and salt.
<<endif>>

===

title: visit_louvre
position: 490,576
group: louvre
tags: actorUID=CRAZY_WOMAN
---
<<asset louvre_inside>>
CRAZY_WOMAN: You can find many sculptures and paintings.
<<jump find_monalisa>>
===

title: find_monalisa
position: 862,430
group: louvre
tags: actorUID=CRAZY_WOMAN
---

<<action monalisa>>
CRAZY_WOMAN: Go and find the Mona Lisa!
===

title: go_bakery
position: 347,60
group: louvre
tags: actorUID=OLD_WOMAN
---
 
OLD_WOMAN: Now look for Antura! I think it went to the bakery for a baguette!
OLD_WOMAN: Hurry up!
===

title: baguette_salt
position: 588,1161
group: bakery
color: yellow
tags: actorUID=CRAZY_MAN
---
<<action COLLECT_1>>
<<asset baguette_salt>>
CRAZY_MAN: This is salt.
===
title: baguette_flour
position: 822,1161
group: bakery
color: yellow
tags: actorUID=CRAZY_MAN
---
<<asset baguette_flour>>
<<action COLLECT_2>>
CRAZY_MAN: This is flour.
===


title: baguette_water
position: 323,1162
group: bakery
color: yellow
tags: actorUID=CRAZY_MAN
---
<<action COLLECT_3>>
<<asset baguette_water>>
CRAZY_MAN: This is water.
===

title: baguette_yeast
position: 76,1156
group: bakery
color: yellow
tags: actorUID=CRAZY_MAN
---
<<action COLLECT_4>>
<<asset baguette_yeast>>
CRAZY_MAN: This is yeast.
===

title: talk_notre_dame_roof
position: 365,-209
group: notredame
tags: actorUID=OLD_MAN
---
<<asset notredame_fire>>
OLD_MAN: There was a big fire in 2019, but we could repair it.
 
I saw Antura run into the Louvre Museum.
It is just across the River Seine.
===


title: talk_louvre_guide
position: 491,281
group: louvre
tags: actorUID=CRAZY_WOMAN, type=Choice
color: blue
---
CRAZY_WOMAN: Welcome to the Louvre Museum. What do you want to do?
-> Tell me about the Louvre
    <<jump visit_louvre>>
-> Exit
    <<if $MET_MONALISA>>
        <<action AREA_LOUVRE_EXIT>>
        Come back!    
    <<else>>
        <<jump find_monalisa>>
    <<endif>>
===

title: louvre_monalisa
position: 62,585
group: louvre
color: yellow
tags: actorUID=CRAZY_WOMAN
---
<<asset monalisa>>
CRAZY_WOMAN: This is the famous Mona Lisa.
<<set $MET_MONALISA = true>>
<<asset leaonardodavinci>>
CRAZY_WOMAN: It was painted around 1500 by the artist and scientist Leonardo da Vinci.

===

title: louvre_liberty
position: 64,427
group: louvre
color: yellow
tags: actorUID=CRAZY_WOMAN, 
---
<<asset liberty_leading>>
CRAZY_WOMAN: Liberty Leading the People by the French artist Eugène Delacroix represents freedom.
===

title: louvre_venus
position: 65,268
group: louvre
color: yellow
tags: actorUID=CRAZY_WOMAN, 
---
<<asset venusmilo>>
CRAZY_WOMAN: The Venus de Milo, an ancient Greek marble sculpture.
===

title: npc_woman
position: -626,-94
tags: actorUID=WOMAN
color: purple
---
WOMAN: Ciao. What do you want to know?
-> What is the Eiffel Tower?
    The famous iron tower, 300 meters tall.
    The symbol of Paris!
-> Where are we?
    We are in Paris, the city of love!
-> Is this place real?
    Sure! Why are you asking?
    Well... it looks like a video game, doesn't it?
-> Nothing. Bye.
===


title: npc_man
position: -628,-253
tags: actorUID=MAN
color: purple
---
MAN: Do you have any questions?
-> Have you seen Antura?
    => Yes! Talk to everybody and follow the lights!
    => No. Who is Antura?
-> What are you doing?
    => I'm going to work!
    => I'm going to buy bread at the bakery.
-> Where do you come from?
    => I wasn't born in this country.
    => From planet Earth.
-> Goodbye
===



title: npc_kid
position: -621,-415
tags: actorUID=KID_MALE
color: purple
---
=> Hi!
=> How are you?
===


