//--------------------------------------------
// DEV QUEST SCRIPT
//--------------------------------------------
// Country: Italy - Firenze
// Content: Living document for testing Yarn features
// and documenting available commands
//--------------------------------------------

//--------------------------------------------
// INIT
// every quest starts with an INIT node
// to inizialize existing variables and declare new ones
//--------------------------------------------

// title of a node must be unique to this quest
// create a new node for anything that must be referenced multi times
// that makes understanding of the flow easier
title: init
// position is used to place the node in the Graph view in the Editor
position: 0,0
// group is used to organize the nodes in the Graph view in the Editor
group: docs
// tags are used to add metadata to the node
//  - actor is the speacking character, used mostly for voice overs
tags: actor=Guide
// color us used like this:
//  - red for important nodes like init, quest end, tasks
//  - blue for NPCs
//  - yellow for items
//  - green for tasks
//  - purple for activities
color: red
---
// these are the common variables
<<set $EASY_MODE = false>>
<<set $IS_DESKTOP = false>>
<<set $TOTAL_COINS = 0>>
<<set $COLLECTED_ITEMS = 0>>
<<set $MAX_PROGRESS = 0>>
<<set $CURRENT_PROGRESS = 0>>
<<set $CURRENT_ITEM = "">>

// here we declare new variables for this quest
<<declare $met_teacher = false>>
<<declare $got_hint = false>>
<<declare $doorUnlocked = false>>
===

//--------------------------------------------
// COMMANDS
// these are the commands available in the game
//--------------------------------------------

title: docs_commands
position: 240,1
group: docs
color: red
---
// adds the item = cardata to the inventory (needs to be collectable!)
<<inventory id_cardata>> // same as add
<<inventory id_cardata add>>
<<inventory id_cardata remove>>
// launches an activity
<<activity id_activity_configuration>>
// starts a task
<<task_start id_task_configuration>>
// end a task... if you say nothing, its a success
<<task_end id_task_configuration>>
<<task_end id_task_configuration fail>>
// or even without the id to end current task
<<task_end>>
<<task_end fail>>
// shows the image
<<asset id_asset>> 
// hides the current image
<<asset_hide>>
// configured in the actiosManager
<<action action_id>>
// called to end the current quest
<<quest_end 3>>
// called to end the current quest with n (1,2,3) stars!
// <<quest_end n>>
// if the quest is not completed
// <<quest_end fail>>
===

//--------------------------------------------
// FUNCTIONS
// these are the functions available in the game
//--------------------------------------------

title: docs_functions
position: 483,8
color: red
group: docs
---
// these are the functions available in the game

<<if item_count("flag_france") >= 2>>
    You have more than 2 Flags of France
<<endif>>

<<if has_item("flag_france")>>
    You have a Flag of France
<<endif>>

<<if has_item_at_least("flag_france", 3)>>
    You have at least 3 Flags of France
<<endif>>

<<if can_collect("flag_france")>>
    You can collect a Flag of France
<<endif>>

===

title: test_inventory
position: 6,244
group: inventory
---
I am hungry. How about some food?
<<if has_item_at_least("fr_baguette", 3)>>
    I needed 3 baguettes!
    <<inventory fr_baguette remove>>
    <<inventory fr_baguette remove>>
    <<inventory fr_baguette remove>>
<<else>>
    You don't have enough baguettes!
    <<if has_item("fr_fish")>>
        but you have some fish!
    <<endif>>
<<endif>>

===
title: got_baguette
position: 489,243
group: inventory
---
You found a baguette!
<<inventory fr_baguette add>>

===
title: got_fish
position: 252,245
group: inventory
---
An healthy fish!
<<inventory fr_fish add>>

===
title: NPCcard
position: 8,482
group: cards: 
---
ora sblocco una carta ok?
<<card fr_fish>>
torna al menù e guarda il book
e ora la baguette
<<card fr_baguette>>
Buona vera?

===
title: camera_test
position: 316,479
group: camera 
---
Now i show you the map of the playgroud
<<camera_focus map_tutorial>>
Do you see that point?
<<camera_focus map_tutorial_detail>>
Now Look at that palace!
<<camera_focus palace>>
Nice isnt' it?
<<camera_reset>>

===
title: endgame
position: -368,1
group: endgame
color: red
---

You can end this quest with...
-> Zero stars (fail)
    <<quest_end 0>>
-> 1 star
    <<quest_end 1>>
-> 2 stars
    <<quest_end 2>>
-> 3 stars
    <<quest_end 3>>

===

title: NPCGIoconda
position: 662,2326
assetimage: antura_hero
---
<<asset food_tomato>>
ciao, stefano
ti piacciono le foto?
<<asset food_fish>>
-> vuoi arancia?
    <<asset food_orange>>
    <<action play_sfx>>
-> vuoi olio?
    <<asset food_oil>>
    <<action play_sfx>>
ora chiudo
<<asset_hide>>
-> Activity Order
    <<activity activity_test>>
    <<jump activity_done>>
-> Suona sfx
    <<action play_sfx>>
-> Apri Baule
    <<action open_chest>>

===
title: activity_done
position: 918,2328
---
Activity finished.
===

title: NPCGreeting2
position: 304,2331
assetimage: pirates
---
// <<activity activity_memory>>
Narrator: Hallo
Narrator: How are you?
-> Good
    Narrator: Good to hear that
-> Bad
    Narrator: Oh, I am sorry to hear that
-> Neutral
    Narrator: I see, I hope it gets better soon
Narrator: What do you want to do with  coins?
===
title: NPCGreeting3
position: 31,2331
tags: #camera2 background:conductor_cabin
---

<<declare $coins = 0>>
Narrator: What do you want to do with {$coins} coins? 
-> Open the door
 <<set $doorUnlocked = true>>
 Narrator: The door is open
-> Stay at home
 Narrator: You stay at home
 <<set $coins = $coins + 10>>
===
title: Chest_01
position: 142,2543
---
<<if $doorUnlocked>>
Narrator: la porta è aperta
<<jump EntroInCasa>>
<<else>>
Narrator: la porta è chiusa
<<endif>>
===

title: note_for_the_editors
style: note
position: -109,-200
---
Custom note
this is useful to leave notes for the editors
like reminders of things to do
or things to check
or things to fix
===

title: EntroInCasa
position: 425,2541
---
entro in casa

===


title: coin_machine
position: 29,2122
tags:
group: variables
---
I'll give you 100 coins.
<<set $TOTAL_COINS = $TOTAL_COINS + 100>>

===

title: quiz_test
position: 4,718
group: quiz
tags: actor=GUIDE, type=QUIZ
---
Calculate 3*2
-> 4
    <<jump quiz_test_wrong>>
-> 6
    <<jump quiz_test_correct>>
-> 8
    <<jump quiz_test_wrong>>
===

title: quiz_test_correct
position: 316,696
group: quiz

---
YES! Correct.
===

title: quiz_test_wrong
position: 319,847
group: quiz

---
GUIDE: No. Do you want to try again?
-> Yes
    <<jump quiz_test>>
-> No
===


title: test_mood
position: 746,2539
tags: actor=GUIDE, mood=HAPPY
---
Happy Mood

===

title: random_lines
position: 32,1891
tags: actor=OLD_WOMAN
group: random
---
Smart random Line 1
Smart random Line 2
Smart random Line 3
===

title: multiple_lines
position: 331,1899
tags: actor=KID_FEMALE
group: random
---
=> This is Hint 1
=> This is Hint 2
=> Hint 3

===

title: activity_test
position: 13,1104
tags: actor=KID_FEMALE
group: activities
color: purple
---
KID_FEMALE: Play this NORMAL Test Activity!
<<activity  ACTIVITY_TEST>>
===

title: chest_money
position: 312,1106
tags: actor=TUTOR
group: activities
color: purple
---
TUTOR: Play with money
<<activity  ACTIVITY_MONEY easy>>
===





title: switch_door
position: 13,1365
tags: actor=TUTOR
group: actions
color: yellow
---
TUTOR: and closes POST
<<action CLOSE_DOOR>>
===

title: actions_test
position: 285,1369
tags: actor=KID_FEMALE
group: actions
---
<<action PRE_ACTIONS>>
Action Pre "pre_actions" and action_post "post_actions"
<<action POST_ACTIONS>>
===


title: task_collect_flowers
position: 18,1624
tags: actor=KID_FEMALE
group: tasks
color: green
---
Collect 5 flowers!
<<task_start COLLECT_FLOWERS>>
===


