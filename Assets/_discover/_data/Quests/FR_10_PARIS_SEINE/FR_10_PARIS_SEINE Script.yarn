// FR-10 Paris - Seine
title: init
position: 0,0
tags: type=Panel
color: red
---
<<set $TOTAL_COINS = 0>>
<<set $CURRENT_PROGRESS = 0>>
<<set $MAX_PROGRESS = 8>>
Welcome to the River Seine in Paris!
You will learn about the river, its bridges, and the boats that sail on it.
===

title: talk_tutor
position: 270,1
tags: type=Condition
color: blue
---
<<if $CURRENT_PROGRESS >= $MAX_PROGRESS>>
<<jump found_all_photos>>
<<elseif $CURRENT_PROGRESS > 0>>
Keep searching for the photos!
<<else>>
<<jump welcome>>
<<endif>>

===

title: welcome
position: 269,203
tags: actor=TUTOR, asset=seine_river_panoramic
---
TUTOR: This is the River Seine!
<<card  seine_map>>
TUTOR: The River Seine flows through Paris.
TUTOR: Many people live by this river.
<<jump task_seine>>
===

title: found_all_photos
position: 586,-3
tags: actor=TUTOR
---
TUTOR: Thank you!
TUTOR: Do you know that there are 37 bridges on the Seine?
<<jump look_seine_map>>
===

title: task_seine
position: 567,210
tags: actor=TUTOR
---
<<task_start seine>>
TUTOR: I lost 8 photos of the Seine.
Can you find them all?
Then come back to me!
===


title: sign_train_bridge
position: -15,-430
color: yellow
tags: actor=TUTOR
---
<<card sign_train_bridge>>
<<action COLLECT_1>>
TUTOR: This is a bridge for trains!
===

title: sign_car_bridge
position: 229,-428
tags: actor=TUTOR
color: yellow
---
<<card sign_car_bridge>>
<<action COLLECT_2>>
TUTOR: This is a bridge for cars!
===


title: sign_people_bridge
position: 469,-424
tags: actor=TUTOR, 
color: yellow
---
<<card sign_people_bridge>>
<<action COLLECT_3>>
TUTOR: This is a bridge for people!
===

title: boat_people
position: -1,-255
tags: actor=TUTOR
color: yellow
---
<<card  boat_people>>
<<action COLLECT_5>>
TUTOR: This boat has large windows, so tourists can see the city. It's a 'bateau-mouche' (a tourist boat).
===

title: boat_house
position: 721,-429
tags: actor=TUTOR,
color: yellow
---
<<card boat_house>>
<<action COLLECT_6>>
TUTOR: This boat is a house! People live here!
===

title: boat_goods
position: 730,-249
tags: actor=TUTOR, boat_goods
color: yellow
---
<<card boat_goods>>
<<action COLLECT_7>>
TUTOR: This boat transports goods.
===

title: look_seine_map
position: 863,-3
tags: actor=TUTOR, asset=
---
<<card  seine_france_map>>
TUTOR: Now look at this map of the Seine.
<<jump question_seine_map>>
===

title: question_seine_map
position: 1135,-4
tags: actor=TUTOR, type=Choice
---
TUTOR: The Seine flows into a sea. Which one?
-> "The Mediterranean Sea":
    <<jump question_wrong>>
-> "The English Channel":
    <<jump question_correct>>
-> "The Atlantic Ocean":
    <<jump question_wrong>>
===

title: question_correct
position: 1137,211
tags: actor=TUTOR
color: purple
---
TUTOR: Yes! The Seine flows into the English Channel, in northern France.
<<card  seine_map>>
TUTOR: On this map, name the monuments you recognize.
TUTOR: Draw a bridge in your notebook! Do you remember the Pont Alexandre III?
TUTOR: Great, you finished this level.
<<quest_end 3>>
===

title: question_wrong
position: 1363,-135
tags: actor=TUTOR
---
TUTOR: Hmm... not really. Look at the map again.
<<jump question_seine_map>>
===

title: npc_train
position: -274,-128
tags: actor=MAN
---
=> I would love to ride a train!
=> I love trains!
===

title: npc_boat
position: -271,28
tags: actor=MAN
---
=> No, I didn't see your dog.
=> A dog? There are many dogs here...
=> I did see a dog walking around, yes.
===


title: boat_eiffel
position: 472,-249
tags: actor=TUTOR, meta_ACTION_POST=COLLECT_8, meta=ACTION_POST:COLLECT_8, asset=boat_eiffel
color: yellow
---
TUTOR: Look how nice!
===

title: boat_river
position: 239,-255
tags: actor=TUTOR, meta_ACTION_POST=COLLECT_4, meta=ACTION_POST:COLLECT_4, asset=boat_river
color: yellow
---
TUTOR: There are many boats.
===

title: talk_ile_de_france
position: -278,-435
tags: actor=WOMAN
---
<<card ile_de_france>>
Look: there is an island, Île de la Cité.
It is the historic center of Paris.
===

title: talk_pont_alexandre
position: -273,-285
tags: actor=WOMAN
---
<<card alexander_iii_bridge>>
This is the Pont Alexandre III, a historic monument.
===


