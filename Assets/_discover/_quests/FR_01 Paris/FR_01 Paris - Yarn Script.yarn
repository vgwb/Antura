// FR_01 PARIS
// Cards:
// - tour_eiffell (landmark education)
// - mr_eiffel (historical figure)
// - tour_eiffell_ticket (cultural practice)
// - tour_eiffell_map (navigation aid)
// - louvre (landmark education)
// - louvre_inside (cultural immersion)
// - monalisa (art education)
// - leaonardodavinci (historical figure)
// - liberty_leading (art education)
// - venusmilo (art education)
// - notredame (landmark education)
// - notredame_fire (recent history)
// - baguette (cultural food)
// - baguette_flour (ingredient education)
// - baguette_water (ingredient education)
// - baguette_yeast (ingredient education)
// - baguette_salt (ingredient education)
// Tasks:
// - Collect 4 baguette ingredients from various locations
// - Reach Monalisa
// Activities:
// - puzzle - Eiffel Tower
// Words: 
// ticket, coin, baguette, flour, water, yeast, salt, map, elevator, iron, museum, cathedral, painting, sculpture,

title: init
position: 0,0
tags:
type: panel
color: red
---
<<set $TOTAL_COINS = 0>>
<<set $COLLECTED_ITEMS = 0>>
<<declare $QUEST_ITEMS = 0>>
<<declare $MET_GUIDE = false>>
<<declare $MET_MAJOR = false>>
<<declare $MET_MONALISA = false>>

Welcome to Paris! #line:fr01_start 
Go and talk with the tutor! #line:fr01_start_2
<<target tutor>>
===



title: talk_tutor
position: 300,-663
actor: tutor
color: blue
---
I saw Antura going to the Eiffel Tower. #line:fr01_talk_tutor
<<camera_focus tour_eiffell>>
Follow the target or use the map! #line:fr01_talk_tutor_2 
Get there as soon as possible! #line:fr01_talk_tutor_3 
===


title: talk_louvre_external
position: 320,61
group: louvre
actor: OLD_WOMAN
---
<<if $MET_MONALISA>>
    <<jump go_bakery>>
<<else>>
<<asset louvre>>
This is the entrance to the Louvre, our national art museum. #line:0cf1cc8 
Do you want to enter? #line:0f74ff9
-> Yes: #line:090114f 
    Enjoy your visit! #line:056e051 
    <<action AREA_LOUVRE_ENTER >>
-> No: #line:077422a 
    Okay. #line:0c28ea0 
<<endif>>
===

title: talk_eiffell_roof
position: 319,-444
group: toureiffel
tags: actor=GUIDE
---
<<asset toureiffell>>
The Eiffel Tower is 300 meters tall. #line:08c1973 
<<asset mr_eiffel>>
Built by Mr Eiffel in 1887. #line:09e5c3b 
<<asset iron>>
It's made of iron! #line:0d59ade 
I saw Antura going towards Notre Dame. #line:04d1e52 
<<camera_focus notredame>>
Get there! #line:083b3bf 

===


title: talk_eiffell_guide
position: 584,-443
group: toureiffel
actor: guide
---
<<if $TOTAL_COINS > 2>>
    Here is your ticket. #line:04e74ad 
    <<asset tour_eiffell_ticket>>
    <<set $TOTAL_COINS = $TOTAL_COINS-3>>
    <<action area_toureiffel>>
    <<asset tour_eiffell_map>>
    I saw Antura going up to the top of the tower. #line:089abda 
    Take the elevator! #line:0585a5e 
<<elseif $TOTAL_COINS > 0>> 
    Collect all coins! #line:04d966b 
<<else>>
    The ticket for the Eiffel Tower costs 3 coins. #line:069cbb3 
<<endif>>
===

title: talk_notre_dame
position: 327,-203
group: notredame
tags: actor=OLD_MAN, asset=notredame
---
I'm the Mayor of Paris. #line:0cc11fa 
This is Notre-Dame Cathedral. #line:06f3fa2 
<<asset notredame>>
It is a famous Gothic church, built in 1182. #line:02edc0f 
<<action AREA_NOTREDAME_ROOF>>
Come with me up to the roof of the church! #line:083dfcc 
<<set $MET_MAJOR = true>>
===

title: talk_cook
position: 334,976
group: bakery
actor: CRAZY_MAN
---
<<if $COLLECTED_ITEMS >= 4>>
    GREAT! I can now bake the Baguette. And... #line:0017917 
    CONGRATULATIONS! You won the game! Did you like it? #line:0d11596 
    <<quest_end 3>>
<<else>>
    Help me! Antura made a mess in my kitchen! #line:07bbb10 
    I can't find the ingredients to make the baguette. #line:09e867c 
    <<asset  baguette>>
    Our special French bread! #line:0874503 
    <<set $QUEST_ITEMS = 4>>
    Please bring me 4 ingredients: #line:07d64c7 
    flour, water, yeast, and salt. #line:0c01530 
<<endif>>

===

title: visit_louvre
position: 748,580
group: louvre
tags: actor=CRAZY_WOMAN
---
<<asset louvre_inside>>
You can find many sculptures and paintings. #line:08dc97f 
<<jump find_monalisa>>
===

title: find_monalisa
position: 1120,434
group: louvre
tags: actor=CRAZY_WOMAN
---

<<action monalisa>>
Go and find the Mona Lisa! #line:0442392 
===

title: go_bakery
position: 605,64
group: louvre
tags: actor=OLD_WOMAN
---
 
Now look for Antura! It went to the bakery for a baguette! #line:076ef0f 
Hurry up! #line:0e9c3e7 
===

title: baguette_salt
position: 846,1165
group: bakery
color: yellow
tags: actor=CRAZY_MAN
---
<<action COLLECT_1>>
<<card baguette_salt>>
This is salt. #line:00f1d2f 
===


title: baguette_flour
position: 1080,1165
group: bakery
color: yellow
tags: actor=CRAZY_MAN
---
<<action COLLECT_2>>
<<card baguette_flour>>
This is flour. #line:06022b0 
===


title: baguette_water
position: 581,1166
group: bakery
color: yellow
tags: actor=CRAZY_MAN
---
<<action COLLECT_3>>
<<card baguette_water>>
This is water. #line:0c4d1f6 
===

title: baguette_yeast
position: 334,1160
group: bakery
color: yellow
tags: actor=CRAZY_MAN
---
<<action COLLECT_4>>
<<card baguette_yeast>>
This is yeast. #line:025865d 
===

title: talk_notre_dame_roof
position: 638,-205
group: notredame
tags: actor=OLD_MAN
---
<<asset notredame_fire>>
There was a big fire in 2019, but we could repair it. #line:09a0ead 
I saw Antura run into the Louvre Museum. #line:02ba888 
It is just across the River Seine. #line:00d22e5 
===


title: talk_louvre_guide
position: 749,285
group: louvre
tags: actor=CRAZY_WOMAN
color: blue
---
Welcome to the Louvre Museum. What do you want to do? #line:0e6d2a5 
-> Tell me about the Louvre #line:0a5fc63 
    <<jump visit_louvre>>
-> Exit #line:0efc18f 
    <<if $MET_MONALISA>>
        <<action AREA_LOUVRE_EXIT>>
        Come back! #line:07dd921 
    <<else>>
        <<jump find_monalisa>>
    <<endif>>
===

title: louvre_monalisa
position: 320,589
group: louvre
color: yellow
tags: actor=CRAZY_WOMAN
---
<<asset monalisa>>
This is the famous Mona Lisa. #line:louvre_monalisa_1
<<set $MET_MONALISA = true>>
<<asset leaonardodavinci>>
It was painted around 1500 #line:louvre_monalisa_2
by the artist and scientist Leonardo da Vinci. #line:louvre_monalisa_3
===

title: louvre_liberty
position: 322,431
group: louvre
color: yellow
tags: actor=CRAZY_WOMAN
---
<<asset liberty_leading>>
This painting represents freedom. #line:louvre_liberty_1
It's called Liberty Leading the People #line:louvre_liberty_2
by the French artist Eugène Delacroix #line:louvre_liberty_3
===

title: louvre_venus
position: 323,272
group: louvre
color: yellow
tags: actor=CRAZY_WOMAN, 
---
<<asset venusmilo>>
The Venus de Milo, an ancient Greek marble sculpture. #line:053d4fe 
===

title: npc_woman
position: -284,321
tags: actor=WOMAN
color: purple
---
Ciao. What do you want to know? #line:0070084 
-> What is the Eiffel Tower? #line:0d91dc0 
    The famous iron tower, 300 meters tall. #line:0f17af0 
    The symbol of Paris! #line:07a113f 
-> Where are we? #line:09dd1da 
    We are in Paris, the city of love! #line:02b627d 
-> Is this place real? #line:08bede4 
    Sure! Why are you asking? #line:08654e6 
    Well... it looks like a video game, doesn't it? #line:0bc62a3 
-> Nothing. Bye. #line:0fe0732 
===


title: npc_man
position: -286,162
tags: actor=MAN
color: purple
---
Do you have any questions? #line:07b94e9 
-> Have you seen Antura? #line:0f18ad3 
    => Yes! Talk to everybody and follow the lights! #line:0cf9b4e 
    => No. Who is Antura? #line:0f9dd62 
-> What are you doing? #line:002796f 
    => I'm going to work! #line:0fe4ff4 
    => I'm going to buy bread at the bakery. #line:05a38a8 
-> Where do you come from? #line:05eabcf 
    => I wasn't born in this country. #line:0635a6a 
    => From planet Earth. #line:0749690 
-> Goodbye #line:0ee51fc 
===



title: npc_kid
position: -279,0
tags: actor=KID_MALE
color: purple
---
=> Hi! #line:0c4d9e4 
=> How are you? #line:032d401 
===
