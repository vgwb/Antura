//--------------------------------------------
// DEV QUEST SCRIPT
//--------------------------------------------
// Country: Italy - Firenze
// Content: Living document for testing Yarn features
// and documenting available commands
//--------------------------------------------

//--------------------------------------------
// INIT
// every quest starts with an INIT node
// to inizialize existing variables and declare new ones
//--------------------------------------------

// title of a node must be unique to this quest
// create a new node for anything that must be referenced multi times
// that makes understanding of the flow easier
title: init
// position is used to place the node in the Graph view in the Editor
position: 0,0
// group is used to organize the nodes in the Graph view in the Editor
group: docs
// tags are used to add metadata to the node
// - actor is the speacking character, used mostly for voice overs
// - type is used to identify special nodes like Panel, Quiz
tags: actor=Guide, type=Panel
// color us used like this:
//  - red for important nodes like init and quest end
//  - blue for NPCs
//  - yellow for items
//  - green for tasks
//  - purple for activities
color: red
type: panel
---
// these are the common variables
<<set $EASY_MODE = false>>
<<set $IS_DESKTOP = false>>
<<set $TOTAL_COINS = 0>>
<<set $COLLECTED_ITEMS = 0>>
<<set $MAX_PROGRESS = 0>>
<<set $CURRENT_PROGRESS = 0>>
<<set $CURRENT_ITEM = "">>

// here we declare new variables for this quest
<<declare $met_teacher = false>>
<<declare $got_hint = false>>
<<declare $doorUnlocked = false>>

Welcome to the DEV quest! #line:0973921 
You can use this quest to test features and commands. #line:0196a6d 
===

//--------------------------------------------
// COMMANDS
// these are the commands available in the game
//--------------------------------------------

title: docs_commands
position: 240,1
group: docs
color: red
---

// execute the configured action in the actionManager
<<action action_id>>

// camera commands
<<camera_focus palace>>
<<camera_reset>>

<<card food_baguette show>>
<<card_hide>>
// shows the image
<<asset id_asset>> 
// hides the current image
<<asset_hide>>

// adds the item = card to the inventory (needs to be collectable!)
<<inventory id_card add>>
<<inventory id_card>> // same as add
<<inventory id_card remove>>

// starts a task
<<task_start id_task_configuration>>
// end a task... if you say nothing, its a success
<<task_end id_task_configuration>>
<<task_end id_task_configuration fail>>
<<task_end>> // or even without the id to end current task


// launches an activity
<<activity id_activity_configuration>>

// end current quest and calculate stars based on progress
<<quest_end>>
// end the current quest with n (0, 1,2,3) stars
<<quest_end 3>>

// --- Yarn built-in commands ---
<<wait 3>> // waits for 3 seconds
<<stop>> // stops the current dialogue>>

===

//--------------------------------------------
// FUNCTIONS
// these are the functions available in the game
//--------------------------------------------

title: docs_functions
position: 483,8
color: red
group: docs
---
// these are the functions available in the game

<<if item_count("flag_france") >= 2>>
    You have more than 2 Flags of France #line:08a1058 #native
    another line #line:0563122 
<<endif>>

<<if has_item("flag_france")>>
    You have a Flag of France #line:kidm_0b932d0 
<<endif>>

<<if has_item_at_least("flag_france", 3)>>
    You have at least 3 Flags of France #line:0d694f1 
<<endif>>

<<if can_collect("flag_france")>>
    You can collect a Flag of France #line:03da430 
<<endif>>

===

title: test_inventory
position: 6,244
group: inventory
---
I am hungry. how about some food? #line:0bdf566 
<<if has_item_at_least("food_baguette", 3)>>
    I need 3 baguettes! #line:00c341e 
    <<inventory food_baguette remove>>
    <<inventory food_baguette remove>>
    <<inventory food_baguette remove>>
<<else>>
    You don't have enough baguettes! #line:0c0d786 
    <<if has_item("food_fish")>>
        But you have some fish! #line:06c9370 
    <<endif>>
    Thank you. #line:0a7921b 
<<endif>>

===
title: got_baguette
position: 489,243
group: inventory
---
You found a baguette! #line:dev_got_baguette 
<<inventory food_baguette add>>

===
title: got_fish
position: 252,245
group: inventory
---
An healthy fish! #line:dev_got_fish 
<<inventory food_fish add>>

===
title: area_change
position: 8,482
group: area 
---
Now i'll let you go around. #line:0aa3b5d 
<<area full_area>>

===
title: test_cards
position: 8,482
group: cards 
---
Now i show you some cards #line:05edfca 
<<card food_fish>>
Please look at its details #line:0adbe4f 
<<card food_fish zoom>>
go back to the menu and browse the cards #line:017a620 
and now... #line:04ade19 
<<card food_baguette>>
Good, isnt' it? #line:04b02ce 
<<card_hide>>
GO back to work now. #line:0970ed1 

===
title: test_camera
position: 316,479
group: camera 
---
Now i show you the map of the playgroud #line:0c58bd8 
<<camera_focus map_tutorial>>
Do you see that point? #line:03e0224 
<<camera_focus map_tutorial_detail>>
Now Look at that palace! #line:0c1b2e4 
<<camera_focus palace>>
Nice isnt' it? #line:04daace 
<<camera_reset>>

===
title: test_actions
position: 271,1745
tags: actor=KID_FEMALE
group: actions
---
<<action activate_elevator>>
Action Pre "pre_actions" and action_post "post_actions" #line:0a67b25 
Do you want to open the Chest? #line:03cae39 
-> Yes #line:067231b 
    <<action open_chest>>
-> No #line:05d9657 
<<action stop_elevator>>
===

title: task_collect_apples
position: 4,2000
tags:
group: tasks
color: green
---
<<if HasCompletedTask("collect_apples")>>
    Thank you! you finished this task #line:dev_task_collect_apples_2
<<elseif GetCollectedItem("collect_apples") > 0 >>
    I need more apples! #line:dev_task_collect_apples_3
<<else>>
    Collect 4 apples! #line:dev_task_collect_apples_1 
    <<task_start collect_apples task_collect_apples_done>>
<<endif>>
===

title: task_collect_apples_done
position: 4,2000
tags:
group: tasks
color: green
---
Well done! Go back to the robot #line:dev_test_task_done_1 
===


title: task_open_chest
position: 4,2000
tags:
group: tasks
color: green
---
<<if HasCompletedTask("open_chest")>>
    Thanks you... I needed that #line:073d718 
<<else>>
    Reach and open that chest! #line:0c31fab 
    <<area off>>
    <<target target_chest>>
    <<task_start open_chest task_open_chest_done>>
<<endif>>
===
title: task_open_chest_done
position: 4,2000
tags:
group: tasks
color: green
---
Well done! You opened the chest.  #line:04e697a 
<<target off>>
===

title: item_chest
position: -1,1741
tags: actor=TUTOR
group: actions
color: yellow
---
A good chest! #line:dev_item_chest_1 
<<action open_chest>>
===

title: test_quiz
position: 4,718
group: quiz
tags: type=quiz
---
Calculate 3*2 #line:dev_test_quiz_1 
-> 4 #line:dev_test_quiz_2 
    <<jump test_quiz_wrong>>
-> 6 #line:dev_test_quiz_3 
    <<jump test_quiz_correct>>
-> 8 #line:dev_test_quiz_4 
    <<jump test_quiz_wrong>>
===

title: test_quiz_correct
position: 316,696
group: quiz

---
YES! Correct. #line:0eb010e 
===

title: test_quiz_wrong
position: 319,847
group: quiz

---
GUIDE: No. Do you want to try again? #line:0e2b8d7 
-> Yes #line:08ff2b9 
    <<jump test_quiz>>
-> No #line:042881c 
===


title: endgame
position: -368,1
group: endgame
color: red
---
You can end this quest with... #line:027bdca 
-> NOOOO i don't want end
-> YES, let's end
    <<quest_end>>
    <<jump close_game>>
===

title: close_game
position: -368,200
group: endgame
color: red
type: panel_endgame
---

THis is the last message! goodybye

===

title: NPCGIoconda
position: 648,2702
assetimage: antura_hero
---
<<asset food_tomato>>
ciao, stefano #line:0c0329c 
ti piacciono le foto? #line:0b9b60d 
<<asset food_fish>>
-> vuoi arancia? #line:0c2bd41 
    <<asset food_orange>>
    <<action play_sfx>>
-> vuoi olio? #line:034ed48 
    <<asset food_oil>>
    <<action play_sfx>>
ora chiudo #line:0baeb5f 
<<asset_hide>>
-> Activity Order #line:0087ab1 
    <<activity activity_test>>
    <<jump activity_done>>
-> Suona sfx #line:0f02889 
    <<action play_sfx>>
-> Apri Baule #line:053f800 
    <<action open_chest>>

===
title: activity_done
position: 904,2704
---
Activity finished. #line:0ce187d 
===

title: test_setactive
position: 732,2915

---
Now i'll activate gameObject test_setactive #line:0ef2850 
<<SetActive TestSetActive_Crate>>
And now i'll switch it off #line:0a360b1 
<<SetActive TestSetActive_Crate false>>
Nice, isn't it? #line:06bd992 
===

title: NPCGreeting2
position: 290,2707
assetimage: pirates
---
// <<activity activity_memory>>
Narrator: Hallo #line:08ecb6c 
Narrator: How are you? #line:00b7839 
-> Good #line:0991140 
    Narrator: Good to hear that #line:0b3477e 
-> Bad #line:08607a4 
    Narrator: Oh, I am sorry to hear that #line:0e6ecb2 
-> Neutral #line:05f3ea5 
    Narrator: I see, I hope it gets better soon #line:0802baf 
Narrator: What do you want to do with  coins? #line:0bcd506 
===
title: NPCGreeting3
position: 17,2707
tags: #camera2 background:conductor_cabin
---

<<declare $coins = 0>>
Narrator: What do you want to do with {$coins} coins?  #line:0a1994c 
-> Open the door #line:0e11bee 
 <<set $doorUnlocked = true>>
 Narrator: The door is open #line:0cac01d 
-> Stay at home #line:07f4239 
 Narrator: You stay at home #line:079b16e 
 <<set $coins = $coins + 10>>
===
title: Chest_01
position: 128,2919
---
<<if $doorUnlocked>>
Narrator: la porta è aperta #line:0e22a89 
<<jump EntroInCasa>>
<<else>>
Narrator: la porta è chiusa #line:05086bc 
<<endif>>
===

title: note_for_the_editors
style: note
position: -109,-200
---
Custom note #line:04d621e 
this is useful to leave notes for the editors #line:0520fb4 
like reminders of things to do #line:078fe99 
or things to check #line:0982259 
or things to fix #line:096379b 
===

title: EntroInCasa
position: 411,2917
---
entro in casa #line:0f92df5 

===


title: coin_machine
position: 15,2498
tags:
group: variables
---
I'll give you 100 coins. #line:06427f9 
<<set $TOTAL_COINS = $TOTAL_COINS + 100>>

===

title: test_mood
position: 732,2915
tags: actor=GUIDE, mood=HAPPY
---
Happy Mood #line:0a6e648 

===

title: random_lines
position: 18,2267
tags: actor=OLD_WOMAN
group: random
---
Smart random Line 1 #line:07193d8 
Smart random Line 2 #line:06fe9a7 
Smart random Line 3 #line:032e777 
===

title: multiple_lines
position: 317,2275
tags: actor=KID_FEMALE
group: random
---
=> This is Hint 1 #line:0423295 
=> This is Hint 2 #line:00d69e5 
=> Hint 3 #line:08966ba 

===

title: activity_canvas
position: 11,1139
group: activities
color: purple
---
Activity CANVAS!  #line:0532f99 
<<activity activity_canvas activity_canvas_result>>
===

title: activity_canvas_result
position: 10,1281
group: activities
color: purple
---
<<if GetActivityResult("activity_canvas") > 0>>
You played CANVAS activity well! #line:04fd2ce 
<<else>>
Try again. #line:0797e1e 
<<endif>>
===

title: activity_jigsaw
position: 271,1139
group: activities
color: purple
---
Activity JIGSAW PUZZLE!  #line:0d97ed9 
<<activity activity_jigsaw activity_jigsaw_result>>
===

title: activity_jigsaw_result
position: 270,1283
group: activities
color: purple
---
<<if GetActivityResult("activity_jigsaw") > 0>>
You played JIGSAW PUZZLE activity well! #line:041075b 
<<else>>
Try again. #line:0921ca5 
<<endif>>
===

title: activity_memory
position: 540,1140
group: activities
color: purple
---
Activity MEMORY!  #line:0727144 
<<activity activity_memory activity_memory_result>>
===

title: activity_memory_result
position: 537,1286
group: activities
color: purple
---
<<if GetActivityResult("activity_memory") > 0>>
You played MEMORY activity well! #line:03121d7 
<<else>>
Try again. #line:0d146ef 
<<endif>>
===

title: activity_match
position: 769,1139
group: activities
color: purple
---
Activity MATCH!  #line:0949b00 
<<activity activity_match activity_match_result>>
===

title: activity_match_result
position: 765,1289
group: activities
color: purple
---
<<if GetActivityResult("activity_match") > 0>>
You played MATCH activity well! #line:0426701 
<<else>>
Try again. #line:07643ad 
<<endif>>
===

title: activity_money
position: 1005,1141
group: activities
color: purple
---
Activity MONEY!  #line:05b81d1 
<<activity activity_money activity_money_result>>
===

title: activity_money_result
position: 1011,1293
group: activities
color: purple
---
<<if GetActivityResult("activity_money") > 0>>
You played MONEY activity well! #line:0a63bb1 
<<else>>
Try again. #line:02b103d 
<<endif>>
===

title: activity_order
position: 1253,1142
group: activities
color: purple
---
Activity ORDER!  #line:08e289f 
<<activity activity_order activity_order_result>>
===

title: activity_order_result
position: 1244,1284
group: activities
color: purple
---
<<if GetActivityResult("activity_order") > 0>>
You played ORDER activity well! #line:05527db 
<<else>>
Try again. #line:04681e2 
<<endif>>
===



title: activity_piano
position: 1486,1143
tags: actor=KID_FEMALE
group: activities
color: purple
---
Play this PIANO Melody! #line:0e11b77 
<<activity activity_piano activity_piano_result>>
===

title: activity_piano_result
position: 1486,1285
tags: actor=KID_FEMALE
group: activities
color: purple
---
<<if GetActivityResult("activity_piano") > 0>>
You played the PIANO activity well! #line:0b9f1af 
<<else>>
Try again. #line:04c91cd 
<<endif>>
===



